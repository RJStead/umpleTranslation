use delete_MandatoryFromOptionalOne.ump;
use delete_ManyFromMN.ump;
use delete_ManyFromMany.ump;
use delete_ManyFromOne.ump;
use delete_ManyFromOptionalOne.ump;
use delete_ManyFromX_comp.ump;
use delete_OneFromMany.ump;
use delete_OneFromOne.ump;
use delete_OneFromOptionalOne.ump;
use delete_OptionalOneFromMN.ump;
use delete_OptionalOneFromMN_comp.ump;
use delete_OptionalOneFromMany.ump;
use delete_OptionalOneFromMany_comp.ump;
use delete_OptionalOneFromN.ump;
use delete_OptionalOneFromOne.ump;
use delete_OptionalOneFromOne_comp.ump;
use delete_OptionalOneFromOptionalOne.ump;
use delete_UndirectionalMany.ump;
use delete_UndirectionalOne.ump;


class UmpleToPhp {
    delete_All <<!<</*delete_All*/>><<#

  appendln(realSb,"");
  appendln(realSb,"  public function delete()");
  append(realSb,"  {");

  boolean hasSomethingToDelete = false;

  String customDeletePrefixCode = GeneratorHelper.toCode(uClass.getApplicableCodeInjections("before","delete"));
  String customDeletePostfixCode = GeneratorHelper.toCode(uClass.getApplicableCodeInjections("after","delete"));

  if (customDeletePrefixCode != null) 
  {
    append(realSb, "\n{0}",GeneratorHelper.doIndent(customDeletePrefixCode, "    "));
    hasSomethingToDelete = true;
  }

  for (AssociationVariable av : uClass.getAssociationVariables()) 
  {
    AssociationVariable relatedAssociation = av.getRelatedAssociation();
    
    if (!av.getIsNavigable() || av.isImmutable())
    {
      continue;
    }
    
    if (!relatedAssociation.getIsNavigable())
    {
      if (av.isOne())
      {
        hasSomethingToDelete = true;
        #>><<@ UmpleToPhp.delete_UndirectionalOne >><<#
      }
      else if (av.isMany())
      {
        hasSomethingToDelete = true;
        #>><<@ UmpleToPhp.delete_UndirectionalMany >><<#
      }
      continue;
    }
    
    if (relatedAssociation.getIsComposition()) {
    	if ((relatedAssociation.isOnlyOne() || relatedAssociation.isOptionalOne()) && av.isOnlyOne()) {
    		#>><<@ UmpleToPhp.delete_OneFromOne >><<#
    	}
    	else if ((relatedAssociation.isOnlyOne() || relatedAssociation.isOptionalOne()) && av.isOptionalOne()) {
    		#>><<@ UmpleToPhp.delete_OptionalOneFromOne_comp >><<#
    	}
    	else if ((relatedAssociation.isOnlyOne() || relatedAssociation.isOptionalOne()) && av.isMany()) {
    		#>><<@ UmpleToPhp.delete_ManyFromX_comp >><<#
    	}
    	
    	else if (relatedAssociation.isMany() && av.isOnlyOne()) {
    		#>><<@ UmpleToPhp.delete_OneFromOne >><<#
    	}
    	else if (relatedAssociation.isMany() && av.isOptionalOne()) {
    	    #>><<@ UmpleToPhp.delete_OptionalOneFromOne_comp >><<#
    	}
	else if (relatedAssociation.isMany() && av.isMany()) {
    	    #>><<@ UmpleToPhp.delete_ManyFromX_comp >><<#
    	}
    	else {
    		continue;
    	}
    	
    	
    }
    
    else if (av.getIsComposition() && (av.isMany() && relatedAssociation.isOnlyOne())) { 
    	// normally this is the same for compositions, except for ManyToOne, the
    	// list has to be cleared on the Many end (here, the ones are successfully deleted, 
    	// but they are not removed from the list)
  		// this functionality is not present for compositions, since, in the case
  		// of deleting one from many, this would be on the composition end, and OneFromOne would
  		// be called so the object is actually deleted. 	
  
    	#>><<@ UmpleToPhp.delete_ManyFromX_comp >><<#
	  
    }
    
    else if (av.getIsComposition() && (av.isMany() && relatedAssociation.isOptionalOne() && !av.isMandatory())) { 
    	
    	#>><<@ UmpleToPhp.delete_OptionalOneFromMany_comp >><<#
	  
    }
    
    else if (av.getIsComposition() && (av.isOptionalOne() && relatedAssociation.getMultiplicity().getLowerBound() > 1  && relatedAssociation.getMultiplicity().getLowerBound() != relatedAssociation.getMultiplicity().getUpperBound())) { 
    	
    	#>><<@ UmpleToPhp.delete_OptionalOneFromMN_comp >><<#
	  
    }

    else if (av.getIsComposition() && av.isMany() && relatedAssociation.isOptionalOne()) {
    	    #>><<@ UmpleToPhp.delete_OptionalOneFromMany_comp >><<#
    }
    
    else {
    
	    if (av.isOnlyOne() && relatedAssociation.isOnlyOne())
	    {
	      #>><<@ UmpleToPhp.delete_OneFromOne >><<#
	    }
	    else if (av.isOptionalOne() && relatedAssociation.isOptionalOne())
	    {
	      #>><<@ UmpleToPhp.delete_OptionalOneFromOptionalOne >><<#
	    }
	    else if (av.isOnlyOne() && relatedAssociation.isOptionalOne())
	    {
	      #>><<@ UmpleToPhp.delete_OneFromOptionalOne >><<#
	    }
	    else if (av.isOptionalOne() && relatedAssociation.isOnlyOne())
	    {
	      #>><<@ UmpleToPhp.delete_OptionalOneFromOne >><<#
	    }
	    else if (av.isOptionalOne() && relatedAssociation.isOptionalMany())
	    {
	      #>><<@ UmpleToPhp.delete_OptionalOneFromMany >><<#
	    }
	    else if (av.isOptionalOne() && relatedAssociation.isN())
	    {
	      #>><<@ UmpleToPhp.delete_OptionalOneFromN >><<#
	    }
	    else if (av.isOptionalOne() && relatedAssociation.isMN())
	    {
	      #>><<@ UmpleToPhp.delete_OptionalOneFromMN >><<#
	    }
	    else if (av.isOne() && relatedAssociation.isMany())
	    {
	      #>><<@ UmpleToPhp.delete_OneFromMany >><<#
	    }
	    else if (av.isMandatory() && relatedAssociation.isOptionalOne())
	    {
	      #>><<@ UmpleToPhp.delete_MandatoryFromOptionalOne >><<#
	    }
	    else if (av.isMany() && (relatedAssociation.isMN() || relatedAssociation.isN()))
	    {
	      #>><<@ UmpleToPhp.delete_ManyFromMN >><<#
	    }
	    else if (av.isMany() && relatedAssociation.isMany())
	    {
	      #>><<@ UmpleToPhp.delete_ManyFromMany >><<#
	    }
	    else if (av.isMany() && relatedAssociation.isOnlyOne()) 
	    {
	      #>><<@ UmpleToPhp.delete_ManyFromOne >><<#
	    }
	    else if (av.isMany() && relatedAssociation.isOptionalOne())
	    {
	      #>><<@ UmpleToPhp.delete_ManyFromOptionalOne >><<#
	    }
	    else
	    {
	      continue;
	    }
	}
    hasSomethingToDelete = true;
  }
  
  if (!uClass.isRoot() && !"external".equals(uClass.getExtendsClass().getModifier()))
  {
    hasSomethingToDelete = true;
    appendln(realSb,"");
    append(realSb, "    parent::delete();");
  }

  if (customDeletePostfixCode != null) 
  {
    append(realSb, "\n{0}",GeneratorHelper.doIndent(customDeletePostfixCode, "    "));
    hasSomethingToDelete = true;
  }
  
  if (hasSomethingToDelete)
  {
    appendln(realSb, "");
    appendln(realSb, "  }");
  }
  else
  {
    appendln(realSb, "}");
  }
  
  
#>>!>>
}
