/*
  Banking System - sample UML class diagram written in Umple
  Last updated: February 21, 2011
*/
//Namespace for core of the system.
namespace BankingSystem.core;

class FinancialInstitution {
 name;
 1 -- * ReusableFinancialInstrument;
}

class CreditCardCompany{
 isA FinancialInstitution;
}

class Bank{
 isA FinancialInstitution;
 1 -- * Branch;
}

class FinancialInstrument{
}

class ReusableFinancialInstrument{
 isA FinancialInstrument;
 number;
 pin;
 * -> 1 Currency;
}

class CreditCard {
 isA ReusableFinancialInstrument;
 creditLimit;
 name;
}

class DebitCard {
 isA ReusableFinancialInstrument;
}

class Cheque {
 isA FinancialInstrument;
 amount;
 Date date;
 sequenceNumber;
}

class BankAccount{
 isA FinancialInstrument;
 accountNumber;
 balance;
 Float overdraftOrCreditLimit;
 
 1..* -- * DebitCard;
 1 -- * Cheque; 
}

class Currency
{
 code;
 exchangeRate;
}

class Branch
{
 name;
 address;
 1 -- * BankAccount;
}

class Loan{
 isA BankAccount;
}
//$?[End_of_model]$?

//Positioning

class Cheque
{
  position 334 336 189 96;
}

class ReusableFinancialInstrument
{
  position 582 333 171 75;
  position.association Currency__ReusableFinancialInstrument 120,0 115,76;
}

class Currency
{
  position 587 195 170 79;
}

class CreditCard
{
  position 669 467 145 76;
}



class Loan
{
  position 125 473 109 41;
}

class DebitCard
{
  position 530 464 109 45;
}

class FinancialInstrument
{
  position 370 231 136 45;
}

class Bank
{
  position 242 119 109 45;
  position.association Bank__Branch 0,25 100,0;
}

class CreditCardCompany
{
  position 520 119 139 45;
}

class BankAccount
{
  position 71 328 218 92;
  position.association BankAccount__DebitCard 200,92 0,21;
  position.association BankAccount__Cheque 218,20 0,12;
}

class Branch
{
  position 75 178 127 75;
  position.association BankAccount__Branch 39,75 43,0;
}

class FinancialInstitution
{
  position 359 15 133 58;
  position.association FinancialInstitution__ReusableFinancialInstrument 114,58 0,10;
}